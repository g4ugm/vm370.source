DIB      TITLE 'DMKDIB     (CP)        VM/370 - RELEASE 6'              00001000
         ISEQ  73,80          VALIDATE SEQUENCING OF SYSIN              00002000
         COPY  OPTIONS                                                  00003000
         COPY  LOCAL                                                    00004000
         EJECT                                                          00005000
DMKDIB   START                                                          00006000
         SPACE                                                          00007000
         DC    CL8'DMKDIB'   PAGEABLE MODULE IDENTIFIER                 00008000
         SPACE                                                          00009000
         USING PSA,R0                                                   00010000
         USING VMBLOK,R11                                               00011000
         USING SAVEAREA,R13                                             00012000
         SPACE                                                          00013000
         EXTRN DMKDIADR                                        @V407510 00014000
         EXTRN DMKSTKIO                                        @V407510 00015000
         EXTRN DMKSYSRM                                        @V407510 00016000
         EJECT                                                          00924000
*.                                                                      00925000
* MODULE NAME -                                                         00926000
*                                                                       00927000
*        DMKDIBSM - SIMULATE STATUS FOR NOT-YET-DIALED LINE OR FOR      00928000
*                   NOT-YET-COUPLED CHANNEL-TO-CHANNEL ADAPTER          00929000
*                                                                       00930000
* FUNCTION -                                                            00931000
*                                                                       00932000
*        DMKDIBSM WILL SIMULATE SENSE DATA AND STATUS FOR VIRTUAL       00933000
*        I/O TO A SIMULATED I/O DEVICE (2702 LINE OR CTCA) WHICH        00934000
*        HAS NOT YET BEEN 'ACTIVATED' THROUGH EITHER THE CONSOLE        00935000
*        FUNCTION 'DIAL' (FOR 2702 LINES) OR THE CONSOLE FUNCTION       00936000
*        'COUPLE' (FOR VIRTUAL CTCA'S).                                 00937000
*                                                                       00938000
* ENTRY POINT -                                                         00939000
*                                                                       00940000
*        DMKDIBSM                                                       00941000
*                                                                       00942000
* ENTRY CONDITIONS -                                                    00943000
*                                                                       00944000
*        GPR  2 = 0 (FOR 2702 LINE) OR 'TYPCTCA' (FOR CTCA)             00945000
*               OR TYP3277 FOR 3270                                     00946000
*        GPR  8 = DISPLACEMENT (FROM VMDVSTRT) TO VDEVBLOK              00947000
*        GPR 10 = ADDRESS OF IOBLOK AND CCW PACKAGES FOR I/O            00948000
*        GPR 11 = VMBLOK ADDRESS                                        00949000
*        GPR 12 = ADDRESS OF DMKDIBSM                                   00950000
*        GPR 13 = ADDRESS OF STANDARD SAVE-AREA                         00951000
*                                                                       00952000
* EXIT CONDITIONS -                                                     00953000
*                                                                       00954000
*        GPRS 0-15 UNCHANGED                                            00955000
*        IOBLOK HAS BEEN UPDATED AND STACKED VIA 'DMKSTKIO'             00956000
*                                                                       00957000
* CALLS TO OTHER ROUTINES -                                             00958000
*                                                                       00959000
*        DMKSTKIO - TO STACK THE IOBLOK FOR HANDLING BY DMKVIO          00960000
*        DMKFREE  - TO OBTAIN STORAGE FOR AN IOERBLOK, IF NEEDED        00961000
*        DMKDIADR - TO DROP A DIALED LINE                               00962000
*                                                                       00963000
* EXTERNAL REFERENCES -       NONE                                      00964000
*                                                                       00965000
* TABLES / WORK AREAS -       IOBLOK                                    00966000
*                                                                       00967000
* REGISTER USAGE -                                                      00968000
*                                                                       00969000
*        GPR 13 = SAVE-AREA ADDRESSABILITY                              00970000
*        GPR 12 = MODULE BASE ADDRESSABILITY                            00971000
*        GPR 11 = VMBLOK ADDRESSABILITY                                 00972000
*        GPR 10 = IOBLOK ADDRESSABILITY                                 00973000
*        GPR  9 = RCWCCW ADDRESSABILITY                                 00974000
*        GPR  8 = VDEVBLOK ADDRESSABILITY                               00975000
*        GPR  7 = CSW CCW ADDRESS                                       00976000
*        GPR  4 = CSW RESIDUAL COUNT                                    00977000
*        GPRS 0-3, 5-6 ARE WORK REGISTERS                               00978000
*                                                                       00979000
         EJECT                                                          00980000
* OPERATION -                                                           00981000
*                                                                       00982000
*        DMKDIBSM EXAMINES EACH CCW IN THE CCW STRING TO DETERMINE      00983000
*        WHAT STATUS AND/OR ACTION SHOULD BE TAKEN TO SIMULATE THE      00984000
*        ACTUAL OPERATION OF THE SIMULATED DEVICE BEING PROCESSED.      00985000
*        FOR A NOT-YET-DIALED 2702 LINE:                                00986000
*              A. IF CCW IS A SENSE, SIMULATE SENSE DATA, ADVANCE       00987000
*              B. IF CCW IS A NO-OP, ADVANCE                            00988000
*              C. IF CCW IS AN ENABLE, SET UP FOR PROCESSING BY D       00989000
*                 'DMKDIAL' WHEN 'DIAL' COMMAND IS ISSUED.              00990000
*              D. IF CCW IS NONE OF THE ABOVE, PRESENT UNIT CHECK,      00991000
*                 INTERVENTION REQUIRED.                                00992000
*        FOR AN ALREADY-DIALED LINE: (CCW STRING CONTAINS A DISABLE)    00993000
*              A. IF CCW IS NOT A DISABLE, ADVANCE                      00994000
*              B. IF CCW IS A DISABLE, CALL DMKDIADR TO DROP LINE       00995000
*                 AND RESUME PROCESSING AS IF LINE IS NOT DIALED        00996000
*        FOR A NOT-YET-COUPLED CTCA:                                    00997000
*              A. IF CCW IS A SENSE, SIMULATE SENSE DATA, ADVANCE       00998000
*              B. IF CCW IS A NO-OP, ADVANCE                            00999000
*              C. IF CCW IS NONE OF THE ABOVE, PRESENT UNIT CHECK,      01000000
*                 INTERVENTIONREQUIRED.                                 01001000
*        FOR ALL CASES, AN INVALID CCW WILL CAUSE A PROGRAM CHECK       01002000
*        A SENSE INTO PROTECTED CORE WILL CAUSE A PROTECTION CHECK      01003000
*        2702 LINES NEVER PRODUCE CC = 1 TO SIO, CTCA'S WILL.           01004000
*.                                                                      01005000
         EJECT                                                          01006000
DMKDIBSM RELOC ,    SIMULATE ENDING STATUS FOR NOT-YET-DIAL-ED LINE     01007000
         USING IOBLOK,R10                                               01008000
         USING RCWCCW,R9                                                01009000
         USING VDEVBLOK,R8                                              01010000
         AL    R8,VMDVSTRT    RE-COMPUTE VDEVBLOK ADDRESS               01011000
         SPACE                                                          01012000
         SLR   R4,R4          SET RESIDUAL COUNT REGISTER ZERO          01013000
         SLR   R6,R6          SET IOERBLOK POINTER ZERO, ALSO           01014000
         ST    R6,SAVEWRK1    SET STATUS AND SENSE AREA ZERO            01015000
         MVI   SAVEWRK1,CE+DE INITIALIZE FOR ENDING STATUS              01016000
         MVC   IOBCSW(8),ZEROES    CLEAR IOBCSW                         01017000
         SPACE                                                          01018000
         L     R9,IOBCAW      FIRST CCW ADDRESS                         01019000
         TM    IOBCAW,X'0F'   CAW BITS 4-7 ZERO?               @VA02143 01020000
         BC    5,DIAPRGC1     NO, CHAN. PROG. CK.              @VA02143 01021000
DIACCWS  EQU   *              INTERPRET CCW STRING                      01022000
         LA    R7,8(0,R9)     ADDRESS FOR CSW                           01023000
         ICM   R4,B'0011',RCWCNT   RESIDUAL COUNT FOR CSW               01024000
         TM    RCWCTL,RCWINVL IS THIS AN INVALID CCW ?                  01025000
         BO    DIASTAT        YES - CHECK FOR WHY                       01026000
         CLI   RCWCOMND,X'08' IS THIS A TIC ?                           01027000
         BE    DIALTIC        YES - DO THE TRANSFER                     01028000
         CLI   RCWCOMND,X'18' IS IT TIC AFTER CHAIN DATA ?              01029000
         BE    DIALTIC        YES - GO DO IT                            01030000
         TM    RCWFLAG,X'03'  ARE THE 'MUST-BE-ZERO' BITS ZERO?@VA02143 01031000
         BNZ   DIAPRGC1       NO, CHAN. PROG. CK.              @VA02143 01032000
         CLI   SAVER2+3,TYPCTCA    CALLED FOR VIRT. CTCA ?              01033000
         BE    DIALNOP        YES - DON'T WORRY ABOUT ENABLE, DISABLE   01034000
         CLI   SAVER2+3,TYP3277 3270 DEVICE ??                 @V200730 01035000
         BE    DIALNOP        YES, DONT TEST FOR DISABLE       @V200730 01036000
         CLI   RCWCOMND,X'2F' IS THIS A DISABLE ?                       01037000
         BE    DISABLE        YES - VERY SPECIAL TYPE CCW               01038000
         TM    VDEVFLAG,VDEVDIAL   IS THE LINE STILL DIALED ?           01039000
         BO    DIALADV        YES - SKIP OTHER CHECKING FOR NOW         01040000
         CLI   RCWCOMND,X'27' IS THIS A RE-ENABLE ?                     01041000
         BE    RENABLE        YES - SPECIAL STUFF HERE, TOO             01042000
DIALNOP  EQU   *              CHECK FOR SENSE OR NO-OP COMMAND          01043000
         TM    RCWCOMND,B'00001011'     IS IT A SENSE ?                 01044000
         BZ    DIASENS        YES - PROCESS IT SPECIALLY                01045000
         TM    RCWCOMND,X'03' CONTROL-TYPE COMMAND ?                    01046000
         BNO   DIASTAT        NO -- GIVE UNIT CHECK, INT. REQ.          01047000
         TM    RCWCOMND,X'0C' NO-OP OR SOME OTHER CONTROL ?             01048000
         BNZ   DIASTAT        SOME OTHER KIND - UNIT CHECK              01049000
         TM    RCWFLAG,CC     CHAINED NOP ?                    @VA03261 01050000
         BZ    DIALCC1        NO..CHECK IF FIRST CCW           @VA03261 01051000
DIALADV  EQU   *              ADVANCE TO NEXT CCW                       01052000
         TM    RCWFLAG,CC+CD     CHAINED ?                              01053000
         BZ    DIALCSW        NO - CLEAN ENDING                         01054000
         LR    R9,R7          NEXT ONE IN LINE...                       01055000
         B     DIACCWS        ...AND KEEP ON LOOKING                    01056000
         SPACE                                                          01057000
DIALTIC  EQU   *              PROCESS TRANSFER IN CHANNEL               01058000
         CL    R9,IOBCAW      WORKING ON FIRST CCW?            @VA02143 01059000
         BE    DIAPRGC1       YES, CC=1 PROG. CK.              @VA02143 01060000
         TM    RCWCCW+3,X'07' DOUBLE-WORD ALIGNED?             @VA02143 01061000
         BNZ   DIAPRGC        NO, CHAN. PROG. CK.              @VA02143 01062000
         L     R9,RCWADDR     GET NEXT CCW ADDRESS             @VA02143 01063000
         CLI   RCWCOMND,X'08' TIC TO A TIC?                    @VA02143 01064000
         BE    DIAPRGC        YES, CHAN. PROG. CK.             @VA02143 01065000
         B     DIACCWS        NO, KEEP GOING...                @VA02143 01066000
         SPACE 2                                                        01067000
DIASTAT  EQU   *              SET SENSE BYTE, CSW STATUS                01068000
         CLI   RCWCOMND,X'00' IF OP-CODE IS ZERO,                       01069000
         BNE   DIASTA1        NO -- MUST BE INVALID OP-CODE             01070000
         CL    R9,IOBCAW      WORKING ON FIRST CCW?            @VA02143 01071000
         BNZ   DIAPRGC        NO, CHAN. PROG. CK. WITH CE/DE   @VA02143 01072000
DIAPRGC1 EQU   *              NO C.E./D.E. STATUS              @VA02143 01073000
         MVI   SAVEWRK1,X'00' RESET CE/DE STATUS               @VA02143 01074000
DIAPRGC  EQU   *              YES - CHANNEL PROGRAM CHECK               01075000
         MVI   SAVEWRK1+1,PRGC     ADD THIS TO STATUS                   01076000
         B     DIALCC1        ...AND GO SET THE CSW            @VA02143 01077000
DIASTA1  EQU   *              CHECK FOR INTREQ OR CMDREJ                01078000
         OI    SAVEWRK1,UC    SET UNIT CHECK IN STATUS                  01079000
         MVI   SAVEWRK1+2,INTREQ     SET INTREQ IN SENSE DATA           01080000
         TM    RCWCTL,RCWINVL IS IT ALSO INVALID OP ?                   01081000
         BZ    DIALCSW        NO - ALL SET AS IS                        01082000
         OI    SAVEWRK1+2,CMDREJ   COMMAND REJECT ALSO                  01083000
DIALCSW  EQU   *              CONSTRUCT DUMMY CSW                       01084000
         CLI   SAVER2+3,TYPCTCA    CALLED FROM DMKVCA ?                 01085000
         BE    DIALCC1        YES, CONT                        @V200730 01086000
         CLI   SAVER2+3,TYP3277 3270 DEVICE ??                 @V200730 01087000
         BE    DIALCC1        YES - MIGHT BE CC=1              @VA04021 01088000
         CLI   0(R9),X'03'    NO-OP TO A DIAL LINE?            @VA04021 01089000
         BNE   DIALINT        NO--FORCE CC=0, LATER INTERRUPT  @VA04021 01090000
DIALCC1  DS    0H             CHECK FOR CC=1 WITH STATUS       @VA04021 01091000
         CL    R9,IOBCAW      WORKING ON FIRST CCW ?                    01092000
         BNE   DIALINT        NO -- PRESENT THE ERROR AS AN INTERRUPT   01093000
         CLI   SAVEWRK1+3,X'80'    WAS IT A SENSE COMMAND ?    @V200820 01094000
         BE    DIALINT             YES - CANNOT GIVE CC = 1    @V200820 01095000
         MVI   IOBSTAT,IOBCC1      SET CC = 1 TO SIO                    01096000
         B     DIALSAT        ...AND GO FILL IN CSW STATUS              01097000
         EJECT                                                          01098000
DIALINT  EQU   *              PRESENT STATUS AS AN I/O INTERRUPT        01099000
         NI    VMRSTAT,255-VMIOWAIT     TAKE USER OUT OF IOWAIT         01100000
         STH   R4,IOBCSW+6    RESIDUAL COUNT INTO CSW                   01101000
         ST    R7,IOBCSW      SET CSW ADDRESS...                        01102000
         IC    R1,IOBCAW      GET PROTECTION KEY FROM CAW               01103000
         STC   R1,IOBCSW      ...AND FILL IN CSW                        01104000
DIALSAT  EQU   *              STORE CSW STATUS ONLY                     01105000
         LH    R1,SAVEWRK1    CSW STATUS...                             01106000
         STH   R1,IOBCSW+4    ...                                       01107000
         TM    IOBCSW+4,UC    DID WE CREATE A UNIT CHECK ?              01108000
         BZ    DIALSTK        NO - THAT'S MUCH EASIER                   01109000
         LA    R0,IOERSIZE    GET AN IOERBLOK                           01110000
         CALL  DMKFREE        ...FOR SENSE DATA                         01111000
         LR    R6,R1                                                    01112000
         USING IOERBLOK,R6                                              01113000
         XC    IOERBLOK(IOERSIZE*8),IOERBLOK CLEAR TO ZEROES            01114000
         MVC   IOERCSW(8),IOBCSW   FILL IN CSW                          01115000
         LA    R1,IOERDATA    CONSTRUCT DUMMY SENSE CCW                 01116000
         ST    R1,IOERCCW     ...                                       01117000
         MVI   IOERCCW,X'04'  ...MAKE IT A SENSE                        01118000
         MVC   IOERCCW+4(4),=AL1(SILI,0,0,1) FLAGS+LENGTH               01119000
         MVC   IOERDATA(1),SAVEWRK1+2   SENSE BYTE                      01120000
         DROP  R6                                                       01121000
DIALSTK  EQU   *              SETUP IOBLOK, STACK FOR DMKVIOEX          01122000
         ST    R6,IOBIOER     SET IOERBLOK PTR, IF ANY                  01123000
         ST    R11,IOBUSER    MAKE SURE THIS IS CORRECT                 01124000
         CALL  DMKSTKIO       ...AND STACK THE IOBLOK                   01125000
         EXIT  ,              RETURN TO DMKCCWTR                        01126000
         EJECT                                                          01127000
DIASENS  EQU   *              MIGHT BE A REAL SENSE                     01128000
         MVI   SAVEWRK1+3,X'80'    REMEMBER THE 'SENSE'        @V200820 01129000
         TM    RCWFLAG,SKIP   IF SKIP IS SET...                         01130000
         BO    SETCNT         DMKCCW FAKED IT -- ADJUST COUNT  @VM01012 01131000
         L     R1,RCWADDR     GET DATA ADDRESS                          01132000
         LA    R1,0(0,R1)     STRIP OFF OP-CODE                         01133000
         TM    RCWFLAG,IDA    IS IDA BIT SET ?                          01134000
         BZ    *+8            THANK HEAVENS -- IT'S NOT                 01135000
         L     R1,0(0,R1)     GET ADDRESS FROM IDAL                     01136000
         L     R2,=A(DMKSYSRM) GET REAL MACHINE                @VA03162 01137000
         L     R2,0(,R2)      SIZE...                          @VA03162 01138000
         CLR   R1,R2          VALID ADDRESS                    @VA03162 01139000
         BNL   DIAPRGC        NO..CHANNEL PROGRAM CHECK        @VA03162 01140000
         LR    R2,R1          SAVE THE REAL ADDRESS                     01141000
         CLI   IOBCAW,0       CAW KEY ZERO ?                   @VA03814 01142000
         BE    SETSENS        YES..STORE SENSE BYTE..          @VA03814 01143000
         N     R1,=X'00FFFFF0'     ALIGN FOR AN 'ISK'                   01144000
         ISK   R1,R1          GET THE REAL STORAGE KEY                  01145000
         N     R1,F240        ISOLATE THE FOUR-BIT HUNK                 01146000
         CLM   R1,B'0001',IOBCAW   DOES IT MATCH THE CAW ?              01147000
         BE    SETSENS        YES - LET HIM HAVE SOME DATA              01148000
         MVI   SAVEWRK1+1,PRTC     PROTECTION CHECK                     01149000
         B     DIALCSW        GO SET CSW                                01150000
SETSENS  EQU   *              RETURN SENSE DATA TO USER                 01151000
         MVI   0(R2),X'00'    SENSE BYTE OF ZEROES                      01152000
SETCNT   BCTR  R4,0           ADJUST GR4 FOR RESIDUAL COUNT    @VM01012 01153000
         TM    RCWFLAG,CD     WAS THE SENSE DATA-CHAINED ?     @VM01010 01154000
         BZ    DIALADV        NO -- CHECK FOR CMD CHAIN        @VM01010 01155000
         MVI   SAVEWRK1+1,IL  INCORRECT LENGTH INDICATION      @VM01010 01156000
         B     DIALCSW        GO PRESENT STATUS                @VM01010 01157000
         SPACE 2                                                        01158000
DISABLE  EQU   *              'DISABLE' CCW ENCOUNTERED                 01159000
         MVI   SAVEWRK1+3,X'80' FORCE INTERRUPT IF 1ST CCW     @VA08754 01159500
         MVI   RCWCOMND,X'03' MAKE IT A NO-OP                           01160000
         TM    VDEVFLAG,VDEVDIAL   IS THE LINE ACTIVE ?                 01161000
         BZ    DIALADV        NO - SKIP 'DROP' CALL                     01162000
         CALL  DMKDIADR       DROP THE DIALED LINE                      01163000
         B     DIALADV        GO CONTINUE WITH CCW SCAN                 01164000
         SPACE                                                          01165000
RENABLE  EQU   *              'ENABLE' CCW ENCOUNTERED AFTER 'DISABLE'  01166000
         MVI   RCWCOMND,X'01' CHANGE ENABLE INTO WRITE CIRCLE-C         01167000
         LA    R1,1(0,0)      DATA COUNT                                01168000
         STH   R1,RCWCNT      ...                                       01169000
*  DMKCCWTR HAS ALREADY SET UP THE DATA ADDRESS AND CIRCLE-C BYTE       01170000
         OI    RCWFLAG,SILI   ENSURE NO UNEXPECTED ERRORS               01171000
         ICM   R9,B'1000',IOBCAW   GET ORIGINAL CAW KEY                 01172000
         ST    R9,IOBRCAW     ...AND SET THE RESTART CAW                01173000
         OI    IOBFLAG,IOBRSTRT    ...FOR 'DIAL' SEQUENCE               01174000
         OI    VDEVFLAG,VDEVENAB   DEVICE NOW ENABLED AGAIN             01175000
         ST    R10,VDEVIOB    PRESERVE THE IOBLOK ADDRESS               01176000
         EXIT  ,              ...AND PASS IT BACK TO DMKCCWTR           01177000
         EJECT                                                          01178000
         LTORG                                                          01179000
         EJECT                                                          01180000
         COPY  EQU                                                      01181000
         COPY  DEVTYPES                                                 01182000
         PSA                                                            01183000
         COPY  SAVE                                                     01184000
         COPY  VMBLOK                                                   01185000
         COPY  RBLOKS                                                   01186000
         COPY  VBLOKS                                                   01187000
         COPY  IOBLOKS                                                  01188000
         COPY  IOER                                                     01189000
         END   DMKDIB                                                   01190000