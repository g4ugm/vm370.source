CPF      TITLE 'DMSCPF     (CMS)       VM/370 - RELEASE 6'              00001000
         SPACE 2                                                        00002000
* MODULE NAME -                                                         00003000
*                                                                       00004000
*        DMSCPF (CP)                                                    00005000
*                                                                       00006000
* FUNCTION -                                                            00007000
*                                                                       00008000
*|       TO PASS A VIRTUAL CONSOLE FUNCTION TO CP370 FOR EXECUTION      00009000
*                                                                       00010000
* ATTRIBUTES -                                                          00011000
*                                                                       00012000
*|       NUCLEUS RESIDENT, SERIALLY REUSABLE, CALLED BY BALR OR SVC     00013000
*                                                                       00014000
* ENTRY POINTS -                                                        00015000
*                                                                       00016000
*|       DMSCPF - PASS A VIRTUAL CONSOLE FUNCTION TO CP370              00017000
*                                                                       00018000
* ENTRY CONDITIONS -                                                    00019000
*                                                                       00020000
*|       GPR  0 = LENGTH OF COMMAND LINE (IF IT IS AT CMNDLINE)         00021000
*|       GPR  1 = ADDRESS OF COMMAND LINE                               00022000
*|       GPR 14 = RETURN ADDRESS                                        00023000
*|       GPR 15 = ADDRESS OF DMSCPF                                     00024000
*                                                                       00025000
* EXIT CONDITIONS -                                                     00026000
*                                                                       00027000
*|       NORMAL - CONDITION CODE = 0                                    00028000
*|       GPR 15 = 0                                                     00029000
*                                                                       00030000
*|       ERROR - CONDITION CODE = 2                                     00031000
*|       GPR 15 = ERROR CODE RETURNED FROM CONSOLE FUNCTION             00032000
*                                                                       00033000
* CALLS TO OTHER ROUTINES -                                             00034000
*                                                                       00035000
*|       DMSCWT - TO WAIT FOR ALL CONSOLE I/O TO COMPLETE               00036000
*                                                                       00037000
* EXTERNAL REFERENCES -                                                 00038000
*                                                                       00039000
*        NONE                                                           00040000
*                                                                       00041000
* TABLES / WORKAREAS -                                                  00042000
*                                                                       00043000
*|       BALRSAVE - SAVE AREA FOR CALLER'S REGISTERS 0 - 15             00044000
*|       CMNDLINE - COMMAND LINE MOVED HERE BEFORE CALLING CP           00045000
*                                                                       00046000
* REGISTER USAGE -                                                      00047000
*                                                                       00048000
*|       GPR  0 = WORK REGISTER                                         00049000
*|       GPR  1 = WORK REGISTER                                         00050000
*|       GPR  2 = ADDRESS OF COMMAND LINE                               00051000
*|       GPR  3 = LENGTH OF COMMAND LINE                                00052000
*|       GPR  4 = CONSTANT 1                                            00053000
*|       GPR  5 = END OF COMMAND LINE                                   00054000
*|       GPR  6 = SCRATCH                                               00055000
*|       GPR  7 = CURRENT ARGUMENT SIZE                                 00056000
*|       GPR  8 = CONSTANT 8                                            00057000
*|       GPR  9 = ADDR OF COMMAND LIST                                  00058000
*|       GPR 10 = INDICATES ORIGIN OF COMMAND                           00059000
*|       GPR 11 = NOT USED                                              00060000
*|       GPR 12 = BASE REGISTER                                         00061000
*|       GPR 13 = NOT USED                                              00062000
*|       GPR 14 = RETURN ADDRESS                                        00063000
*|       GPR 15 = RETURN CODE                                           00064000
*                                                                       00065000
* NOTES -                                                               00066000
*                                                                       00067000
*        NONE                                                           00068000
*                                                                       00069000
* OPERATION -                                                           00070000
*                                                                       00071000
*|       IF IT IS NOT ALREADY THERE, DMSCPF MOVES THE COMMAND LINE TO   00072000
*|       'CMNDLINE' COMPRESSING OUT ANY EXCESS BLANKS.  IT THEN CALLS   00073000
*|       DMSCWT TO WAIT FOR ALL CONSOLE I/O TO COMPLETE SO THAT ANY     00074000
*|       OUTPUT FROM THE CP FUNCTION WILL BE SYNCHRONIZED WITH ANY      00075000
*|       PREVIOUS OUTPUT LINES FROM CMS.  IT THEN ISSUES A DIAGNOSE     00076000
*|       INSTRUCTION WITH A CODE OF 8 TO PASS THE CONSOLE FUNCTION TO   00077000
*|       CP TO EXECUTE.  UPON RETURN FROM CP, DMSCPF EXITS WITH THE     00078000
*|       RETURN CODE IN REGISTER 15.                                    00079000
*                                                                       00080000
*.                                                                      00081000
         EJECT                                                          00082000
DMSCPF   START 0              EXECUTE A CP CONSOLE FUNCTION             00083000
         USING NUCON,R0                                                 00084000
         STM   R0,R15,BALRSAVE     SAVE THE CALLER'S REGISTERS          00085000
         LR    R12,R15        LOAD THE PROGRAM BASE REGISTER            00086000
         USING DMSCPF,R12                                               00087000
         LR    R10,R12        INITIALIZE R10 NON-ZERO          @V60BBBB 00087100
         TM    BATFLAGS,BATRUN IS THIS COMMAND FROM BATCH?     @V60BBBB 00087200
         BZ    NOTBATA        BR IF NOT                        @V60BBBB 00087300
         SLR   R10,R10        SET FOR NO PASSWORD-SUPPRESSION  @V60BBBB 00087400
NOTBATA  DS    0H                                              @V60BBBB 00087500
         LA    R1,0(,R1)      MAKE SURE THE HIGH ORDER BYTE IS ZERO     00088000
         LA    R2,CMNDLINE    POINT TO THE COMMAND INPUT LINE           00089000
         CLR   R1,R2          IS THE LINE ALREADY AT 'CMNDLINE' ?       00090000
         BE    CHECKLEN       YES, PASS IT TO CP WITHOUT EDITING        00091000
         SLR   R10,R10        INDICATE FROM EXEC OR MODULE     @V60BBBB 00091050
         TM    BATFLAGS,BATRUN+BATLOAD                            V0742 00091100
         BC    11,NOTBAT           DROP IF BATCH NOT RUNNING      V0742 00091150
         TM    BATFLAGS,BATUSEX                                   V0742 00091200
         BZ    NOTBAT         DROP IF NOT BATCH USER JOB          V0742 00091250
         OI    BATFLAGS,BATCPEX    SIGNAL FOR BATCH ENTRY         V0742 00091300
         LR    R3,R10         SAVE R10 ACROSS BATCH CALL       @V60BBBB 00091320
         L     R15,ABATPROC                                       V0742 00091350
         BALR  R14,R15        GO TO BATCH...                      V0742 00091400
         LR    R10,R3         RESTORE R10                      @V60BBBB 00091420
         LTR   R3,R15         TEST BATCH RET CODE                 V0742 00091450
         BNZ   CPRETURN       NOT ALLOWED BY BATCH                V0742 00091500
         SPACE                                                          00091550
NOTBAT   EQU   *              PROCESS THE CP COMMAND              V0742 00091600
         LA    R3,CMNDLINE    POINT TO THE COMMAND LINE BUFFER          00092000
         LA    R4,1           LOAD THE POINTER INCREMENT                00093000
         LA    R5,131(,R3)    LOAD MAXIMUM BUFFER ENDING ADDRESS        00094000
         SR    R6,R6          CLEAR THE WORK REGISTER                   00095000
         SR    R7,R7          SET ARGUMENT SIZE TO ZERO                 00096000
         LA    R8,8           LOAD THE MAXIMUM ARGUMENT SIZE            00097000
         B     CHECKXFF       MOVE THE COMMAND LINE TO 'CMNDLINE'       00098000
         SPACE                                                          00099000
FINDARGS AR    R1,R4          POINT TO NEXT CHARACTER IN INPUT LINE     00100000
         CLI   0(R1),X'FF'    IS IT THE INPUT END OF THE LINE ?         00101000
         BE    COMPTLEN       YES, COMPUTE LINE LENGTH AND CONTINUE     00102000
         CLI   0(R1),C' '     IS THIS THE START OF AN ARGUMENT ?        00103000
         BE    FINDARGS       NO, LOOK AT THE NEXT CHARACTER            00104000
         B     MOVEBLNK       YES, MOVE A BLANK TO THE COMMAND BUFFER   00105000
         SPACE                                                          00106000
CHECKBSC CLI   0(R1),BS       IS THIS CHARACTER A BACKSPACE ?           00107000
         BNE   MOVEBLNK       NO, CONTINUE NORMALLY                     00108000
         AR    R1,R4          POINT TO NEXT CHARACTER IN INPUT LINE     00109000
         LA    R7,1           SET ARGUMENT LENGTH TO 1                  00110000
         B     FINDARGE       CONTINUE WITHOUT INSERTING A DELIMITER    00111000
         SPACE                                                          00112000
MOVEBLNK MVI   0(R3),C' '     MOVE IN A BLANK DELIMITER                 00113000
         BXH   R3,R4,COMPTLEN POINT TO NEXT CHARACTER IN THE BUFFER     00114000
         SR    R7,R7          RESET ARGUMENT LENGTH TO ZERO             00115000
FINDARGE CLI   0(R1),C' '     IS THIS THE END OF THE ARGUMENT ?         00116000
         BE    FINDARGS       YES, FIND THE START OF THE NEXT ONE       00117000
         CR    R7,R8          IS THIS ARGUMENT THE MAXIMUM LENGTH ?     00118000
         BE    CHECKBSC       YES, ADD A DELIMITER TO THE BUFFER        00119000
         AR    R7,R4          ADD ONE TO THE ARGUMENT LENGTH            00120000
         IC    R6,0(,R1)      MOVE THE CHARACTER TO COMMAND BUFFER      00121000
         STC   R6,0(,R3)      ...                                       00122000
         BXH   R3,R4,COMPTLEN POINT TO NEXT CHARACTER IN THE BUFFER     00123000
         AR    R1,R4          POINT TO NEXT CHARACTER IN INPUT LINE     00124000
CHECKXFF CLI   0(R1),X'FF'    IS THIS THE END OF THE INPUT LINE ?       00125000
         BNE   FINDARGE       NO, CONTINUE SCANNING THE LINE            00126000
COMPTLEN LR    R0,R3          SAVE POINTER TO LAST CHARACTER            00127000
         SR    R0,R2          COMPUTE THE LENGTH OF THE CP COMMAND      00128000
CHECKLEN LTR   R3,R0          CHECK FOR A VALID LENGTH                  00129000
         BNP   CPRETURN       RETURN IF LENGTH IS INVALID               00130000
FINDCHAR CLI   0(R2),C' '     IS THIS CHARACTER A BLANK ?               00131000
         BNE   CHKIMPCP       NO, CHECK FOR IMPLIED CP FUNCTION         00132000
         LA    R2,1(,R2)      POINT TO THE NEXT CHARACTER               00133000
         BCT   R3,FINDCHAR    DECREMENT LENGTH AND CONTINUE             00134000
         B     CPRETURN       RETURN IF NO FUNCTION SPECIFIED           00135000
         SPACE                                                          00136000
CHKIMPCP IC    R6,=C' '       MOVE A BLANK TO END OF COMMAND LINE       00137000
         STC   R6,0(R2,R3)    ...                                       00138000
         CLC   0(3,R2),=C'CP '     IS 'CP' THE FIRST ARGUMENT ?         00139000
         BE    FOUNDCP        YES,GO TO IT                     @VA06272 00140050
         LA    R9,CMNDLIST    POINT TO COMMAND LIST            @VA06272 00140100
         LM    R0,R1,0(R9)    GET THE NAME...                  @VA06272 00140150
         L     R15,=V(ABBREV) TO CHECK FOR SYNONYM...          @VA06272 00140200
         BALR  R14,R15        FOR 'CP'                         @VA06272 00140250
         LTR   R15,R15        SYNONYM FOUND?                   @VA06272 00140300
         BNZ   PASSTOCP       NO                               @VA06272 00140350
         ICM   R1,B'1110',=C'CP ' SHOULD BE 'CP'               @VA06272 00140400
         CLR   R0,R1          IF NOT....                       @VA06272 00140450
         BNE   PASSTOCP       PASS TO CP ANYWAY                @VA06272 00140500
         LR    R1,R2          POINT TO COMMAND LINE            @VA06272 00140550
CKFORBLK EQU   *                                               @VA06272 00140600
         MVI   0(R1),C' '     BLANK OUT SYNONYM....            @VA06272 00140650
         LA    R1,1(,R1)      SO THAT....                      @VA06272 00140700
         CLI   0(R1),C' '     DIAGNOSE IS OK                   @VA06272 00140750
         BE    FINDFUNC       GO TO ISSUE DIAGNOSE             @VA06272 00140800
         B     CKFORBLK       LOOK FOR BLANKS                  @VA06272 00140850
FOUNDCP  EQU   *                                               @VA06272 00140900
         LA    R2,2(,R2)      POINT BEYOND THE 'CP'                     00141000
         S     R3,=F'2'       AND SUBTRACT 2 FROM THE LENGTH            00142000
         BNP   PASSTOCP       BRANCH IF NO FUNCTION SPECIFIED           00143000
FINDFUNC CLI   0(R2),C' '     IS THIS THE START OF THE NEXT ARGUMENT ?  00144000
         BNE   PASSTOCP       YES, PASS THE COMMAND TO CP               00145000
         LA    R2,1(,R2)      POINT TO THE NEXT CHARACTER               00146000
         BCT   R3,FINDFUNC    SUBTRACT 1 FROM THE LENGTH AND CONTINUE   00147000
PASSTOCP LA    R1,=CL8'CONWAIT'    POINT TO DMSCWT PLIST                00148000
         SVC   202            WAIT FOR ALL CONSOLE I/O TO COMPLETE      00149000
         LTR   R10,R10        COMMAND FROM MODULE/EXEC?        @V60BBBB 00149200
         BZ    MODOREX        BR IF YES                        @V60BBBB 00149400
         O     R3,=X'80000000' REQUEST PASSWORD SUPPRESSION    @V60BBBB 00149600
MODOREX  DS    0H                                              @V60BBBB 00149800
         DC    X'83230008'    PASS COMMAND LINE TO CP TO EXECUTE        00150000
CPRETURN LTR   R15,R3         LOAD RETURN CODE AND SET CONDITION CODE   00151000
         LM    R0,R14,BALRSAVE     RESTORE THE CALLER'S REGISTERS       00152000
         BR    R14            RETURN TO THE CALLER                      00153000
         SPACE 3                                                        00154000
BS       EQU   X'16'                                                    00155000
         SPACE                                                          00156000
         LTORG                                                          00157000
         EJECT                                                          00158000
         NUCON                                                          00159000
         REGEQU                                                         00160000
         END                                                            00161000